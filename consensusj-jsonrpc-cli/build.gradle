/*
 * CLI tool support library and `cj-bitcoin-cli` tool.
 * `jsonrpc` tool is compiled to a native command-line tool with Graal `native-image`
 */
plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'de.jjohannes.extra-java-module-info' version "0.1"
}

sourceCompatibility = 9
targetCompatibility = 9

configurations {
    nativeToolImplementation.extendsFrom implementation
}

dependencies {
    api project(':consensusj-jsonrpc')

    api 'commons-cli:commons-cli:1.4'

    // Add SLF4J runtime adapter for JDK logging for GraalVM native-image build of jsonrpc tool
    nativeToolImplementation "org.slf4j:slf4j-jdk14:${slf4jVersion}"

    testImplementation "org.codehaus.groovy:groovy:${groovyVersion}:indy"

    testRuntimeOnly "org.slf4j:slf4j-jdk14:${slf4jVersion}"
}

def mainClassName = "org.consensusj.jsonrpc.cli.GenericJsonRpcTool"

java {
    modularity.inferModulePath = true
}

extraJavaModuleInfo {
    automaticModule("commons-cli-1.4.jar", "org.apache.commons.cli")
    automaticModule("hamcrest-core-1.3.jar", "org.hamcrest")
}

jar {
    manifest {
        attributes  'Implementation-Title': 'ConsensusJ JSON-RPC CLI library and tool',
                    'Main-Class': mainClassName,
                    'Implementation-Version': archiveVersion.get()
    }
}

// Compile a native image using GraalVM's native-image tool
// Graal must be installed at $GRAAL_HOME
task graalNativeImage(type:Exec, dependsOn: jar) {
    workingDir = projectDir
    executable = "${System.env.GRAAL_HOME}/bin/native-image"
    args = [ '--verbose',
             '--no-fallback',
             '-cp', "${-> configurations.nativeToolImplementation.asPath}", // Lazy configuration resolution
             '-jar', jar.archiveFile.get(),
             '-H:Path=build',
             '-H:Name=jsonrpc',
             '--initialize-at-build-time=com.fasterxml.jackson.annotation.JsonProperty$Access',
             '-H:IncludeResources=logging.properties',
             '-H:ReflectionConfigurationFiles=graal-reflection-config.json',
             '-H:EnableURLProtocols=http,https',
             '-H:+ReportUnsupportedElementsAtRuntime',
             '-H:+ReportExceptionStackTraces'
    ]
}
