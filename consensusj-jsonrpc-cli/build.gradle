/*
 * CLI tool support library and `cj-bitcoin-cli` tool.
 * `cj-bitcoin-cli` tool is compiled to a native command-line tool with GraalVM `native-image`
 */
plugins {
    id 'java-library'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}

dependencies {
    compile project(':consensusj-jsonrpc')

    compile 'commons-cli:commons-cli:1.4'

    // Add runtime adapter for JDK logging for shadowJar and GraalVM native-image builds
    runtime "org.slf4j:slf4j-jdk14:${slf4jVersion}"

    testCompile "org.codehaus.groovy:groovy:${groovyVersion}:indy"
}

def mainClassName = "org.consensusj.jsonrpc.cli.JsonRpcTool"
ext.moduleName = 'org.consensusj.jsonrpc.cli'

jar {
    inputs.property("moduleName", moduleName)
    manifest {
        attributes  'Implementation-Title': 'ConsensusJ JSON-RPC CLI library and tool',
                    'Automatic-Module-Name': moduleName,
                    'Main-Class': mainClassName,
                    'Implementation-Version': archiveVersion.get()
    }
}

// Compile a native image using GraalVM's native-image tool
// Graal must be installed at $GRAAL_HOME
task graalNativeImage(type:Exec, dependsOn: jar) {
    workingDir = projectDir
    executable = "${System.env.GRAAL_HOME}/bin/native-image"
    args = [ '--verbose',
             '-cp', configurations.runtime.asPath,
             '-jar', jar.archiveFile.get(),
             '-H:Path=build',
             '-H:Name=jsonrpc',
             '--initialize-at-build-time=com.fasterxml.jackson.annotation.JsonProperty$Access',
             '-H:IncludeResources=logging.properties',
             '-H:ReflectionConfigurationFiles=graal-reflection-config.json',
             '-H:EnableURLProtocols=http,https',
             '-H:+ReportUnsupportedElementsAtRuntime',
             '-H:+ReportExceptionStackTraces'
    ]
}
