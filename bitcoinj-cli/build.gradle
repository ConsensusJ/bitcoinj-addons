// JVM-based CLI tools 
// bitcoinj-cli tool is packaged to a single jar with ShadowJar
// We previously explored the possibility of a native (AOT-compiled) executable using RoboVM
//
plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
}

dependencies {
    compile project(':bitcoinj-rpcclient')

    compile 'commons-cli:commons-cli:1.3.1'
}


mainClassName = "com.msgilligan.bitcoinj.cli.BitcoinJCli"

// Since other projects use this module as a dependency we don't want to publish the shadowjar
// We need to change the build so that we build the executable jar with a new name
//bintrayUpload.dependsOn shadowJar

jar {
    manifest {
        attributes 'Implementation-Title': 'bitcoinj CLI client', 'Implementation-Version': version, 'Consensus-Class': mainClassName
    }
}

shadowJar {
    baseName = 'bitcoinj-cli'
    classifier = ''
}

// Test Structure
sourceSets {
    integrationTest {
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + sourceSets.main.output + configurations.testRuntime
        groovy {
            srcDir 'src/integ/groovy'
        }
        resources {
            srcDir 'src/integ/resources'
        }
    }
}

task regTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    testLogging.showStandardStreams = true
    beforeSuite { descriptor ->
        if (descriptor.getClassName() != null) {
            logger.lifecycle('\033[1m' + descriptor.getName() + "\033[0m") // bold
        }
    }
    beforeTest { descriptor ->
        logger.lifecycle('    ' + descriptor.getName())
    }

    systemProperty 'regtest', true
    systemProperty 'java.util.logging.config.file', "${project.projectDir}/src/integ/logging.properties"
    systemProperties ([ "omni.test.rpcTestUser": rpcTestUser,
                        "omni.test.rpcTestPassword": rpcTestPassword,
    ])
    include 'com/msgilligan/bitcoinj/**'
}
