plugins {
    id 'java-library'
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
    // Note:  jackson-core and jackson-databind have "Automatic-Module-Name" in manifest since 2.9.1
    api "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    api "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"

    testImplementation "org.codehaus.groovy:groovy:${groovyVersion}:indy"
    testImplementation ("org.codehaus.groovy:groovy-json:${groovyVersion}:indy") {
        transitive = false
    }
}

ext.moduleName = 'org.consensusj.jsonrpc'

jar {
    inputs.property("moduleName", moduleName)
    manifest {
        attributes "Automatic-Module-Name": moduleName
    }
}

test {
    testLogging.showStandardStreams = true
    systemProperty 'java.util.logging.config.file', "${project.projectDir}/src/test/logging.properties"
    beforeTest { descriptor ->
        logger.lifecycle('    ' + descriptor.getName())
    }
}
