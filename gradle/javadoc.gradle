ext.javadocSpec = {
    options.docletpath = configurations.asciidoclet.files.asType(List)
    options.doclet = 'org.asciidoctor.Asciidoclet'
    options.addStringOption '-base-dir', "${projectDir}"
    options.addStringOption '-attribute',
            "name=${project.name}," +
                    "version=${project.version}," +
                    "title-link=http://consensusj.github.io/consensusj/[${project.name} ${project.version}]"
    options.addStringOption '-require', 'asciidoctor-diagram'
    options.addStringOption '-gem-path', 'build/vendor'
    options.addStringOption '-attribute', 'data-uri'
    options.addBooleanOption 'linksource', true             // include link to HTML source file
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption 'html5', true              // Requires JDK 9+ and Asciidoclet 1.5.6
    }

    options.links(  'https://docs.oracle.com/javase/8/docs/api/',
                    "https://bitcoinj.github.io/javadoc/0.15/",
                    "https://fasterxml.github.io/jackson-core/javadoc/2.9/",
                    "https://fasterxml.github.io/jackson-databind/javadoc/2.9/")

}

allprojects {
    configurations {
        asciidoclet
    }

    dependencies {
        asciidoclet "org.asciidoctor:asciidoclet:${asciidocletVersion}"
        asciidoclet "org.asciidoctor:asciidoctorj:${asciidoctorjVersion}"
        asciidoclet "org.asciidoctor:asciidoctorj-diagram:${asciidoctorjDiagramVersion}"
    }

    javadoc javadocSpec
}

task javadocAll(type: Javadoc) {
    source subprojects.collect {project -> project.sourceSets.main.allJava }
    classpath = files(subprojects.collect {project -> project.sourceSets.main.compileClasspath})
}
javadocAll javadocSpec << {
    options.overview = "doc/javadoc-overview.adoc"
    inputs.file(options.overview)
}
javadocAll.doLast {
    logger.info "Consolidated JavaDoc generated at <file://${javadocAll.destinationDir}/index.html>"
}
