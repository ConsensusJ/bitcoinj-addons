buildscript {
    repositories {
        jcenter()
	mavenLocal()
    }
    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
        classpath 'com.github.jruby-gradle:jruby-gradle-plugin:1.3.3'
        classpath 'org.ajoberstar:gradle-git:1.3.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
    }
}

ext.groovyVersion = '2.4.7'
ext.slf4jVersion = '1.7.21'

allprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'

    version = bitcoinjAddonsVersion     // set in gradle.properties
    group = 'com.msgilligan'

    repositories {
        jcenter()
	mavenLocal()
    }
}

subprojects {
    dependencies {
        compile 'org.bitcoinj:bitcoinj-core:0.15-SNAPSHOT'
        compile "org.slf4j:slf4j-api:${slf4jVersion}"

        testCompile "org.codehaus.groovy:groovy:${groovyVersion}"
        testCompile("org.spockframework:spock-core:1.1-groovy-2.4-rc-2") {
            exclude module: "groovy-all"
        }

        testRuntime  "org.slf4j:slf4j-jdk14:${slf4jVersion}"          // Runtime implementation of slf4j
    }
}

apply from: 'gradle/idea.gradle'
apply from: 'gradle/javadoc.gradle'
apply from: 'gradle/asciidoctor.gradle'
apply from: 'gradle/github-pages.gradle'
apply from: 'gradle/bintray.gradle'

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}

task jenkinsBuild(dependsOn: [build, testReport, javadocAll, asciidoctor])
