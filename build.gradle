buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
    }
}

ext.groovyVersion = '2.4.4'

allprojects {
    apply plugin: 'java'

    version = bitcoinjAddonsVersion     // set in gradle.properties
    group = 'com.msgilligan'

    repositories {
        jcenter()
    }
}

apply from: 'gradle/idea.gradle'
apply from: 'gradle/bintray.gradle'
apply from: 'gradle/javadoc.gradle'

subprojects {
    dependencies {
        compile "org.bitcoinj:bitcoinj-core:0.13.2"
    }
}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}

task jenkinsBuild(dependsOn: [build, testReport])
